utilizar bepd/builtins
utilizar bepd/datos/conjunto
utilizar bepd/x/adhoc (ObjetoAdHoc)

[DOCUMENTA
@brief{Intercala un elemento en un arreglo.}

Intercala @pd{valor} entre todos los elementos de @pd{arr}. Por ejemplo:

@ejemplo|{
necesitas
    (Intercalar: (Arreglo#crearCon: 1, 2, 3, 4), {=})
    =
    (Arreglo#crearCon: 1, {=}, 2, {=}, 3, {=}, 4)
}|

Puedes imaginarlo como una versión de @pd{Unir} pero para arreglos.
DOCUMENTA]
funcion Intercalar: arr, valor
    variable res
    fijar res a Arreglo#vacio
    ParaCadaElementoConÍndice: arr, procedimiento: elem, i
        si i > 0
            res#agregarAlFinal: valor
        finsi
        res#agregarAlFinal: elem
    finprocedimiento
    devolver res
finfuncion

[DOCUMENTA
@brief{Separa un arreglo en base a un predicado.}

Devuelve un objeto con dos atributos, @code{parteVerdadera} y
@code{parteFalsa}. Ambos son arreglos. @code{parteVerdadera} contiene aquellos
elementos de @pd{arr} para los que @pd{%cond: elemento} devolvió
@pd{VERDADERO}, mientras que @code{parteFalsa} contiene aquellos para los que
devolvió @pd{FALSO}.
DOCUMENTA]
funcion SepararArreglo: arr, cond
    variables parteVerdadera, parteFalsa
    fijar parteVerdadera a Arreglo#vacio
    fijar parteFalsa a Arreglo#vacio
    ParaCadaElemento: arr, procedimiento: el
        si %cond: el
            parteVerdadera#agregarAlFinal: el
        sino
            parteFalsa#agregarAlFinal: el
        finsi
    finprocedimiento
    devolver ObjetoAdHoc: {parteVerdadera}, parteVerdadera,
                          {parteFalsa}, parteFalsa
finfuncion

[DOCUMENTA
@brief{Devuelve un arreglo sin duplicados.}

Devuelve un arreglo como @pd{arr} pero sin elementos duplicados. Además, el
órden de los elementos no está especificado.
DOCUMENTA]
funcion Únicos: arr
    devolver (Conjunto#desdeArreglo: arr)#comoArreglo
finfuncion

[ TODO: Documentar ]
funcion ClonarSuperficialmente: arreglo
    variable arr
    fijar arr a Arreglo#vacio
    ParaCadaElemento: arreglo, procedimiento: el
        arr#agregarAlFinal: el
    finprocedimiento
    devolver arr
finfuncion

[DOCUMENTA
@brief{Inserta un elemento en el índice dado.}

Inserta @pd{valor} en el índice @pd{índice} de forma que @pd{(arreglo#en:
índice) = valor}.
DOCUMENTA]
procedimiento Insertar: arreglo, índice, valor
    necesitas índice >= 0
    necesitas índice < arreglo#longitud
    arreglo#agregarAlFinal: NULO
    variable i
    fijar i a arreglo#longitud - 1
    mientras i > índice
        arreglo#fijarEn: i, (arreglo#en: (i - 1))
        fijar i a i - 1
    finmientras
    arreglo#fijarEn: índice, valor
finprocedimiento

[DOCUMENTA
@brief{Elimina un elemento de un arreglo.}

Elimina el elemento en el índice @pd{índice}.
DOCUMENTA]
procedimiento EliminarElemento: arreglo, índice
    necesitas índice >= 0
    necesitas índice < arreglo#longitud
    ParaCadaNúmero: índice, arreglo#longitud, procedimiento: i
        arreglo#fijarEn: i, (arreglo#en: (i + 1))
    finprocedimiento
    arreglo#redimensionar: (arreglo#longitud - 1)
finprocedimiento
