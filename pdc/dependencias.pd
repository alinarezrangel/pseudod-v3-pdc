utilizar bepd/builtins
utilizar bepd/x/sistemaDeArchivos/rutas (ResolverRutaRelativa)
utilizar bepd/x/datos (TipoDeDatos)

utilizar pdc/cst como CST
utilizar pdc/ast como AST
utilizar pdc/tabla (CrearTipoDeRegistro, Tabla, Índice, NodoDeÁrbol, EntablarÁrbol, EscribirTabla)
utilizar pdc/catamorfismos (Buscando)



[DOCUMENTA
@brief{Referencia a un módulo.}

Contiene dos atributos: @code{paquete} y @code{módulo} (ambos @pd{Texto}s) con
el paquete y módulo al que se está referenciando.
DOCUMENTA]
variable ReferenciaAMódulo
fijar ReferenciaAMódulo a TipoDeDatos: {ReferenciaAMódulo}, {paquete}, {módulo}

[DOCUMENTA
@brief{Información de un módulo.}

Es asociado a cada nodo utilizar de un programa, indicando que módulo el nodo
importa. De esta forma cada pase del compilador no necesita volver a parsear el
módulo importado para saber su paquete y módulo.

@itemlist(
@item{@code{idNodo}: ID del nodo utilizar.}

@item{@code{paquete}: Nombre del paquete importado (un @pd{Texto}).}

@item{@code{módulo}: Nombre del módulo importado (un @pd{Texto}).}
)
DOCUMENTA]
variable InformaciónDeMódulo
fijar InformaciónDeMódulo a CrearTipoDeRegistro: {InformaciónDeMódulo},
    {idNodo}, {paquete}, {módulo},
    (Índice#ÚNICO: {idNodo}),
    (Índice#ORDENADO: {paquete}),
    (Índice#ORDENADO: {paquete}, {módulo})


[DOCUMENTA
@brief{Obtiene el paquete de un nombre importado.}

@pd{ruta} debe ser un @pd{Texto} de la forma @code{paquete/más/módulos}. En
este caso devolvería @code{paquete}.
DOCUMENTA]
funcion PaqueteDeUtilizar: ruta
    variable i
    fijar i a ruta#buscar: 0, {/}
    si EsNulo: i
        __FallarConMensaje: ({No se pudo separar los componentes de ~t}#formatear: ruta)
    finsi
    devolver ruta#parteDelTexto: 0, i
finfuncion

[DOCUMENTA
@brief{Obtiene el módulo de un nombre importado.}

@pd{ruta} debe ser un @pd{Texto} de la forma @code{paquete/más/módulos}. En
este caso devolvería @code{más/módulos}.
DOCUMENTA]
funcion MóduloDeUtilizar: ruta
    variable i
    fijar i a ruta#buscar: 0, {/}
    si EsNulo: i
        __FallarConMensaje: ({No se pudo separar los componentes de ~t}#formatear: ruta)
    finsi
    devolver ruta#parteDelTexto: (i + 1), ruta#longitud
finfuncion

[DOCUMENTA
@brief{Obtiene y anota las dependencias del nodo.}

Anota cada @pd{CST#NodoUtilizar} con un @pd{InformaciónDeMódulo} acerca de que
módulo se está importando.

Además, devuelve un arreglo de @pd{ReferenciaAMódulo}s con las dependencias del
nodo.
DOCUMENTA]
procedimiento DependenciasDelNodo: nodoCst, tablaInf, paquete, nombreDelMódulo
    variable dependencias
    fijar dependencias a Arreglo#vacio
    Buscando: nodoCst, funcion: nodo
        devolver EsInstancia: nodo, CST#NodoUtilizar
    finfuncion, procedimiento: nodo
        variables porImportar, módulo, paq
        fijar porImportar a nodo#módulo
        si EsInstancia: nodo#módulo, CST#NodoIdentificador
            variable ruta
            fijar ruta a nodo#módulo#nombre
            fijar paq a PaqueteDeUtilizar: ruta
            fijar módulo a MóduloDeUtilizar: ruta
        sino
            fijar paq a paquete
            fijar módulo a ResolverRutaRelativa: nombreDelMódulo, nodo#módulo#valor
        finsi
        dependencias#agregarAlFinal: (ReferenciaAMódulo#crear: paq, módulo)
        tablaInf#agregar: (InformaciónDeMódulo#crear: nodo#id, paq, módulo)
    finprocedimiento
    devolver dependencias
finprocedimiento
