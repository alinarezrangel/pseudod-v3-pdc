utilizar bepd/builtins

utilizar pdc/cst como CST
utilizar pdc/ast como AST
utilizar pdc/catamorfismos (Buscando, Buscando')
utilizar pdc/combinadores (Error)
utilizar pdc/parser (ErrorComoTraceback)

[DOCUMENTA
@brief{Valida que solo se llamen a identificadores autoejecutables.}

Falla con un error si en algún momento se llama a un identificador que no es
autoejecutable en el CST dado. @pd{tablasRnCst} deben ser las @pd(#:utilizar
"pdc/nombres"){TablasDeResoluciónDeNombres} del CST.
DOCUMENTA]
funcion ValidaLlamadasAutoejecutables: cst, tablasRnCst, tablaCst
    Buscando': cst, funcion: cst
        devolver (EsInstancia: cst, CST#NodoLlamarProcedimiento)
              || (EsInstancia: cst, CST#NodoSi)
    finfuncion, funcion: cst, continuar
        si EsInstancia: cst, CST#NodoSi
            variable est
            fijar est a tablasRnCst#tablaCondicionalEstático#intentaBuscarÚnico: {idNodo}, cst#id
            si EsNulo: est
                [ Condicional normal ]
                devolver %continuar
            sino
                variable bloque
                fijar bloque a tablaCst#buscarÚnico: {id}, est#idBloque
                ValidaLlamadasAutoejecutables: bloque#nodo, tablasRnCst, tablaCst
                devolver NULO
            finsi
        finsi

        variables reg, binding
        fijar reg a tablasRnCst#tablaUsos#buscarÚnico: {idNodo}, cst#proc#id
        fijar binding a tablasRnCst#tablaNombres#buscarÚnico: {binding}, reg#binding
        si no binding#esAutoejecutable
            Escribir:
                (ErrorComoTraceback:
                    (Error#conMensajeYLugar:
                        ({La variable ~t no es autoejecutable}#formatear: cst#proc#nombre),
                        cst#proc#áreaTextual))
            __FallarConMensaje: {Variable no es autoejecutable}
        finsi
        devolver %continuar
    finfuncion
finfuncion

[DOCUMENTA
@brief{Valida que no halla ninguna instrucción @code{devolver} fuera de una función.}

Falla con un error si en algún lugar del AST se encuentra un @code{devolver}
fuera de una función.
DOCUMENTA]
funcion ValidaDevolverFueraDeFunción: ast
    Buscando: ast, funcion: ast
        devolver (EsInstancia: ast, AST#NodoFunciónAnónima)
              || (EsInstancia: ast, AST#NodoDevolver)
    finfuncion, funcion: ast
        si EsInstancia: ast, AST#NodoDevolver
            Escribir:
                (ErrorComoTraceback:
                    (Error#conMensajeYLugar:
                        {Devolver fuera de una función/método/procedimiento},
                        ast#áreaTextual))
            __FallarConMensaje: {Devolver fuera de una función/método/procedimiento}
        finsi
    finfuncion
finfuncion
